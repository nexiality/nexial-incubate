/*
 * Copyright 2012-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
	ext.kotlin_version = '1.3.60'
	ext.springBootVersion = '2.1.6.RELEASE'
	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id "org.jetbrains.kotlin.jvm" version "1.3.60"
	id 'org.jetbrains.kotlin.plugin.spring' version '1.3.41'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'distribution'

// spring 5
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'io.spring.dependency-management'

group = componentGroup

// BUILD_NUMBER
// org.gradle.project.BUILD_NUMBER=systemPropertyValue
ext.env = System.getenv()
ext.buildNumber = (env.RELEASE_VERSION ?: componentVersion) + "_" +
                  ((env.BUILD_NUMBER ?: "0").toInteger() + "").padLeft(4, '0')
version = ext.buildNumber

targetCompatibility = componentJavaVersion
sourceCompatibility = componentJavaVersion
archivesBaseName = componentName

repositories {
	mavenLocal()
	mavenCentral()
	//	maven { url "http://www.dcm4che.org/maven2" }
	//	maven { url 'https://jitpack.io' }
	// maven { url 'http://maven.top-q.co.il/content/repositories/public/' }
}

// spring 5
springBoot { mainClassName = componentMainClass }
//bootJar { mainClassName = componentMainClass }
bootRun { sourceResources sourceSets.main }

configurations {
	provided
	providedCompile
}

sourceSets.main.compileClasspath += configurations.provided

dependencies {
	compile("org.apache.httpcomponents:httpcore:${httpcoreVersion}")
	compile("org.apache.httpcomponents:httpclient:+")
	compile("org.apache.httpcomponents:fluent-hc:${httpclientFluent}")
	compile("org.apache.httpcomponents:httpmime:+")
	compile("org.apache.httpcomponents:httpasyncclient:${httpasyncclient}")

	// support: apache commons
	compile("org.apache.commons:commons-lang3:+")
	compile("org.apache.commons:commons-collections4:4.3")
	compile("commons-beanutils:commons-beanutils:+")
	compile("commons-cli:commons-cli:+")
	compile("commons-fileupload:commons-fileupload:+")
	compile("commons-io:commons-io:+")
	compile("org.apache.commons:commons-dbcp2:+")
	// compile("org.apache.commons:commons-email:+")
	compile("org.apache.commons:commons-text:+")
	compile("org.apache.commons:commons-compress:+")
	compile("commons-validator:commons-validator:+") {
		exclude module: "commons-collections"
	}

	// csv
	compile("com.univocity:univocity-parsers:+")

	// spring (forces version unification)
	compile("org.springframework.boot:spring-boot-starter")
	compile("org.springframework.boot:spring-boot-configuration-processor")
	compile("org.springframework.boot:spring-boot-starter-jdbc")
	compile("org.springframework.boot:spring-boot-starter-mail")
	// note: can't get rid of jackson completely because it's references in tika and aws-* libs. boo!
	compile("org.springframework.boot:spring-boot-starter-web") {
		exclude module: "jackson-databind"
		exclude module: "jackson-annotations"
		exclude module: "jackson-core"
		exclude module: "jackson-module-parameter-names"
		exclude module: "jackson-datatype-jdk8"
		exclude module: "jackson-datatype-jsr310"
		exclude module: "spring-boot-starter-json"
	}
	compile("org.springframework:spring-tx")
	compile("org.springframework.boot:spring-boot-starter-websocket")
	compile("org.springframework.boot:spring-boot-starter-thymeleaf")
	compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+")

	// unit testing
	compile("junit:junit:${junitVersion}")

	// selenium
	// for IE webdriver: https://developer.microsoft.com/en-us/microsoft-edge/tools/webdriver/
	compile("org.seleniumhq.selenium:selenium-java:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-api:${seleniumVersion}")
	// compile("org.seleniumhq.selenium:selenium-server:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-remote-driver:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-support:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-ie-driver:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-safari-driver:${seleniumVersion}")
	compile("org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}")

	// https://mvnrepository.com/artifact/io.appium/java-client
	//	compile("io.appium:java-client:${appiumVersion}")

	// winium
	// for Winium.Desktop.Driver.exe download: https://github.com/2gis/Winium.Desktop/releases/latest
	compile("com.github.2gis.winium:winium-webdriver:+")
	compile("com.github.2gis.winium:winium-elements-desktop:+")

	// logback lib already references in spring boot logging starter
	compile("ch.qos.logback:logback-access:+")
	compile("ch.qos.logback.contrib:logback-json-core:+")
	//	compile("org.logback-extensions:logback-ext-spring:+")
	compile("org.fusesource.jansi:jansi:1.+")

	//	compile("ch.qos.logback.contrib:logback-mongodb-core:+")

	// support: aws
	compile("com.amazonaws:aws-java-sdk-core:+")
	compile("com.amazonaws:aws-java-sdk-s3:+")
	compile("com.amazonaws:aws-java-sdk-polly:+")
	compile("com.amazonaws:aws-java-sdk-sns:+")
	compile("com.amazonaws:aws-java-sdk-ses:+")
	compile('com.amazonaws:aws-java-sdk-sqs:1.11.632')
	compile("com.amazonaws:aws-java-sdk-sts:+")
	// compile("com.amazonaws:aws-java-sdk-textract:+")
	compile("com.amazonaws:aws-java-sdk-rekognition:+")

	// support: browsermob
	// compile("net.lightbody.bmp:browsermob-proxy:+")
	// compile("net.lightbody.bmp:browsermob-core:+")
	// compile("net.lightbody.bmp:browsermob-rest:+") {
	// 	exclude module: "servlet-api"
	// }

	// support: pdf
	// https://mvnrepository.com/artifact/com.itextpdf/itext7-core
	// compile("com.itextpdf:itext7-core:+")
	compile("com.itextpdf:itextpdf:${itextVersion}")
	compile("com.itextpdf.tool:xmlworker:${itextVersion}")

	compile("org.apache.pdfbox:pdfbox:+")
	compile("org.apache.pdfbox:pdfbox-tools:+")
	compile("org.apache.pdfbox:fontbox:+")
	compile("org.apache.pdfbox:xmpbox:+")

	// support: poi/msoffice
	compile("org.apache.poi:poi:${poiVersion}")
	compile("org.apache.poi:poi-ooxml:${poiVersion}")
	compile("org.apache.poi:poi-ooxml-schemas:${poiVersion}")

	// support: json
	compile("org.json:json:${jsonVersion}")
	compile("com.cedarsoftware:java-util:+")
	compile("com.cedarsoftware:json-io:+")
	compile("com.github.fge:json-schema-validator:+") {
		exclude module: "mailapi"
	}
	compile("com.google.code.gson:gson:${gsonVersion}")
	compile("org.danilopianini:gson-extras:+")
	compile("com.jayway.jsonpath:json-path:+")
	compile("org.jdom:jdom2:+")
	compile("org.jsoup:jsoup:+")
	// v23r* is not compatible with selenium 3.4.0
	compile("com.google.guava:guava:${guavaVersion}")

	// support: ini
	compile("org.ini4j:ini4j:+")

	// support: data sources
	// compile("org.mongodb:mongodb-driver-async:${mongoDriverVersion}")
	compile("org.mongodb:mongo-java-driver:${mongoDriverVersion}")
	compile("mysql:mysql-connector-java:${mysqlVersion}")
	compile("org.postgresql:postgresql:+")
	compile("com.microsoft.sqlserver:mssql-jdbc:${mssqlVersion}")
	compile("org.xerial:sqlite-jdbc:${sqliteVersion}")
	compile("org.hsqldb:hsqldb:${hsqldbVersion}")

	// support: redis
	compile("redis.clients:jedis:${jedisVersion}")

	// support: joda-time
	compile("joda-time:joda-time:+")

	// support: others
	compile("net.sourceforge.jregex:jregex:+")
	compile("org.aspectj:aspectjweaver:+")
	compile("jaxen:jaxen:+")
	compile("xalan:xalan:+")
	compile("io.jsonwebtoken:jjwt:+")
	compile("org.thymeleaf:thymeleaf:+")
	compile("javax.media:jmf:+")
	compile("com.github.stephenc.monte:monte-screen-recorder:+")
	// invalid SSL for dcm4che.org, hence disabling this one for now
	//	compile("xuggle:xuggle-xuggler:5.4")

	// support: jms
	compile("org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1")
	compile("javax.transaction:jta:1.1")
	compile("org.apache.activemq:activemq-client:5.14.5")
	compile("com.rabbitmq.jms:rabbitmq-jms:1.8.1")

	compile("javax.mail:javax.mail-api:${javaxMailVersion}")
	compile("com.sun.mail:smtp:${javaxMailVersion}")

	// https://mvnrepository.com/artifact/pl.touk/throwing-function
	compile("pl.touk:throwing-function:1.3")

	compile("org.apache.tika:tika-core:${tikaVersion}")
	compile("org.apache.tika:tika-parsers:${tikaVersion}")

	// support: ssh, scp, sftp
	compile("com.jcraft:jsch:+")

	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
	// implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version")

	compile("com.googlecode.soundlibs:jlayer:1.0.1.4")

	// console with color
	compile("com.diogonunes:JCDP:2.0.3.1")

	// ashot
	compile("ru.yandex.qatools.ashot:ashot:1.5.4")

	// fmpeg (NOTE: THIS IS IN CONFLICT AGAINST THE SAME isoparser PULLED IN VIA THE TIKA DEPEDNENCIES)
	// https://mvnrepository.com/artifact/org.bytedeco/ffmpeg
	// compile group: 'org.bytedeco', name: 'ffmpeg', version: '4.2.1-1.5.2'
	// compile("org.mp4parser:isoparser:1.9.+")
	compile("com.googlecode.mp4parser:isoparser:1.1.22")

	//	non-distributable libs
	compile fileTree(dir: 'lib', include: '*.jar')

	// provided("javax.servlet:javax.servlet-api:${servletApiVersion}") { force = true }

	// annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	testCompile("org.springframework.boot:spring-boot-starter-test")
	testCompile("junit:junit:${junitVersion}")
	testCompile("org.jetbrains.kotlin:kotlin-test")
	// testCompile("org.jetbrains.kotlin:kotlin-test-junit")
}

jar {
	enabled = true
	baseName = archivesBaseName
	manifest {
		attributes "Implementation-Title": archivesBaseName,
		           "Implementation-Version": version,
		           "Main-Class": componentMainClass
	}
}

tasks.withType(Test) {
	maxParallelForks = 10
	// -Dnexial.home=~/projects/nexial/nexial-core
	//	systemProperty 'BROWSER', System.getProperty('BROWSER', 'firefox')
	systemProperty 'nexial.home', projectDir
}

test {
	maxHeapSize = "256m"
	//	jvmArgs '-XX:MaxPermSize=512m'
	jvmArgs '-Xss24m'
	reports.html.enabled = true
	reports.junitXml.enabled = true
	exclude '**/*Manual*'
	testLogging {
		events "PASSED", "STARTED", "FAILED", "SKIPPED"
	}
}

compileJava.dependsOn(processResources)

tasks.withType(JavaCompile) {
	options.fork = true
	options.incremental = true
	options.compilerArgs += ["-parameters"]
}

task singleDependentJar(type: Jar) {
	manifest {
		attributes "Implementation-Title": archivesBaseName,
		           "Implementation-Version": version,
		           "Main-Class": componentMainClass
	}
	baseName = archivesBaseName
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
	zip64 = true
}

task sourcesJar(type: Jar) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task testJar(type: Jar) {
	classifier = 'tests'
	from sourceSets.test.output
	include('**/*.class')
}

artifacts {
	archives jar
	archives sourcesJar
	//	    archives singleDependentJar
}

task distro {
	group "Build"
	description "build project clean room and update lib/ directory"
	mustRunAfter clean
	dependsOn build
}

distributions {
	main {
		contents {
			into('lib') {
				from jar
				from 'lib'
				from(project.configurations.runtime)
			}
			into('template') {
				from 'template'
			}
			into('bin') {
				from 'bin'
			}
		}
	}
}

compileKotlin {
	kotlinOptions {
		jvmTarget = componentJavaVersion
		javaParameters = true
	}
}

compileTestKotlin {
	kotlinOptions {
		jvmTarget = componentJavaVersion
		javaParameters = true
	}
}
